#!/usr/bin/env fish
# Pull secrets from AWS CLI cache files in order to set them in env vars
# Pass the profile name as an optional argument
# Source the output:
# > aws-export-credentials my-profile | source

if not available aws
  perror "aws cli not installed"
  exit 1
end
if not available jq
  perror "jq not installed"
  exit 1
end

set profile ""
if set -q argv[1]
  pinfo "using profile: $argv[1]" >&2
  set profile "--profile=$argv[1]"
else
  pinfo "using default profile" >&2
end

set auth_as (aws sts get-caller-identity $profile | jq '.Arn')
if test -z "$auth_as"
  perror "Unable to authenticate"
  exit 2
end
pinfo "Authenticated as: $auth_as" >&2

set credentials_file (command ls -t ~/.aws/cli/cache | head -1)
set credentials_file (string join "/" "$HOME/.aws/cli/cache" $credentials_file)

set access_key_id (jq '.Credentials.AccessKeyId' $credentials_file)
set secret_access_key (jq '.Credentials.SecretAccessKey' $credentials_file)
set session_token (jq '.Credentials.SessionToken' $credentials_file)

echo "set -x AWS_ACCESS_KEY_ID $access_key_id"
echo "set -x AWS_SECRET_ACCESS_KEY $secret_access_key"
echo "set -x AWS_SESSION_TOKEN $session_token"

psuccess "done" >&2
